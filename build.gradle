plugins {
    id 'com.android.application'
}

android {
    namespace 'com.schedule.app'
    compileSdk 34

    defaultConfig {
        applicationId "com.schedule.app"
        minSdk 23
        targetSdk 34
        versionCode 6
        versionName "1.6.0"
    }

    signingConfigs {
        release {
            // Ключи берём из переменных окружения (задаются в GitHub Actions через Secrets)
            // При локальной сборке используй: ./gradlew assembleRelease -PKEYSTORE_PATH=... и т.д.
            def keystorePath = System.getenv("KEYSTORE_PATH") ?: project.findProperty("KEYSTORE_PATH") ?: ""
            def keystorePass = System.getenv("KEYSTORE_PASSWORD") ?: project.findProperty("KEYSTORE_PASSWORD") ?: ""
            def keyPass      = System.getenv("KEY_PASSWORD") ?: project.findProperty("KEY_PASSWORD") ?: ""
            def keyAliasVal  = System.getenv("KEY_ALIAS") ?: project.findProperty("KEY_ALIAS") ?: "schedule"

            if (keystorePath) {
                storeFile     file(keystorePath)
                storePassword keystorePass
                keyAlias      keyAliasVal
                keyPassword   keyPass
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // Подписываем release-ключом (постоянный — обновление без переустановки)
            signingConfig signingConfigs.release
        }
        debug {
            // Debug тоже подписываем тем же ключом — иначе при переходе debug→release
            // Android требует переустановки из-за разных подписей
            signingConfig signingConfigs.release
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // OkHttp для DoH запросов
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    // JSON
    implementation 'org.json:json:20231013'
    // FileProvider — нужен для установки APK на Android 7+
    implementation 'androidx.core:core:1.13.1'
}
